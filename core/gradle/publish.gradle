def pomConfig = {
    licenses {
        license {
            name 'GNU General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/gpl-3.0.txt'
            distribution "repo"
        }
    }

    developers {
        developer {
            id 'krakowski'
            name 'Filip Krakowski'
            email 'krakowski@hhu.de'
        }

        developer {
            id 'faruh100'
            name 'Fabian Ruhland'
            email 'fabian.ruhland@uni-duesseldorf.de'
        }
    }

    scm {
        url 'https://github.com/hhu-bsinfo/infinileap'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            groupId project.group
            artifactId rootProject.name
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A modern networking framework based on ucx for Java 16+')
                root.appendNode('name', 'infinileap')
                root.appendNode('url', 'https://github.com/hhu-bsinfo/infinileap')
                root.children().last() + pomConfig
            }
        }
    }
}