plugins {
    // Base Plugins
    id 'application'
    id 'idea'

    // Jextract Plugin
    id "io.github.krakowski.jextract" version "0.1.5"
}

compileJava {
    sourceCompatibility = '16'
    targetCompatibility = '16'
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-XDignore.symbol.file']
    options.forkOptions.jvmArgs += ['-J--illegal-access=permit']
}

applicationName = 'infinileap'
mainClassName = 'de.hhu.bsinfo.infinileap.example.App'

project.startScripts.with {
    doLast {
        // The HotSpot VM uses the SIGSEGV signal for its own purposes,
        // which may interfere with signal handlers installed by the ucx library.
        // Therefore, we turn off signal handlers with this (ugly) hack.
        unixScript.text = unixScript.text.replaceFirst('exec ', 'UCX_ERROR_SIGNALS="" exec ')
    }
}

if (project.hasProperty('installPath')) {
    installDist {
        destinationDir = project.file(project.property('installPath'))
    }
}

jextract.enabled = false

group = 'de.hhu.bsinfo'
version = getProperty('projectVersion')

dependencies {

    // Infinileap
    implementation project(':core')

    // Command Line Application Framework
    implementation 'info.picocli:picocli:4.5.0'

    // Log4j Logger
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'

    // JMH Benchmarking Framework
    implementation 'org.openjdk.jmh:jmh-core:1.27'
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.27'

    // AssertJ
    testImplementation 'org.assertj:assertj-core:3.12.2'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.16'
    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
}

test {
    useJUnitPlatform()
}